/**
 * Package:	Configuration of heartbeat
 * Authors:	Martin Lazar <mlazar@suse.cz>
 *
 * $Id$
 */

{
textdomain "heartbeat";

import "Label";
import "Wizard";
import "Heartbeat";
import "Report";
import "SCR";
import "NetworkDevices";

include "heartbeat/helps.ycp";
include "heartbeat/common.ycp";

string thisnode = "";
string thisnodeip = "";
string othernode = "";

void node_conf_Read()
{
    list<string> nodes = Heartbeat::config["node"]:[];

    map unamemap = (map)SCR::Execute(.target.bash_output, "uname -n");
    thisnode = deletechars(unamemap["stdout"]:"", "\n");

    //read current settings for network
    NetworkDevices::Read();

    //generate the map: static IP -> device
    list<string>devs=NetworkDevices::Locate("BOOTPROTO","static");
    foreach(string dev, devs,
    {
	string ip=NetworkDevices::GetValue(dev,"IPADDR");
	if(ip!=nil && ip != "")thisnodeip=ip;
    });

    foreach(string s, nodes, { if (s != thisnode) othernode = s; });
}

boolean node_conf_Write()
{

    string newnode = (string)UI::QueryWidget(`id("othernode"), `Value);
//     if (newnode == nil || newnode == "") {
// 	Report::Error(_("The other node name is required."));
// 	return false;
//     }
//     if (newnode == thisnode) {
// 	Report::Error(_("The other node name cannot be same as this node name."));
// 	return false;
//     }
    
    list<string> onodes = Heartbeat::config["node"]:[];
    if (size(onodes) != 2 || onodes[0]:"" != thisnode || onodes[1]:"" != newnode) {
	Heartbeat::config["node"] = [ thisnode, newnode ];
	Heartbeat::config["modified"] = true;
    }
    
    return true;
}
    
term node_conf_getDialog()
{
   return `VBox(
            `HBox(
	        `HWeight( 9,
	          `Frame(_("This Node"),
		      `HBox(
		          `Label(thisnode),
// 		          `HStretch(),
		          `Label("("),
		          `Label(thisnodeip),
		          `Label(")")
                      )
                   )
                )
            ),
	    `VSquash(
            `HBox(
                `HWeight( 9,
                    `Frame(_("Add Nodes"),
                        `HBox(
		            `Left(`TextEntry(`id("othernode"),_("Node Name"), othernode)),
			    `HStretch(),
		            `Left(`TextEntry(`id("othernodeip"),_("Node IP")))
                        )
                    )
                ),
                `HWeight( 2,
                    `VBox(
                        `VSpacing(1),
                        `VSquash(
                            `PushButton(`id("add_stonith"), Label::AddButton())),
			`VSpacing(0.5),
			`VSquash(
			    `PushButton(`id("edit_stonith"), Label::EditButton())),
                        `VStretch()
                    )
                )
            )),
	    `HBox(
		`HWeight( 9, 
		    `Table(`id("stonith_table"), `opt(`notify),
			`header("Node Name", "Node IP"))
		),
		`HWeight( 2,
		    `VBox(
			`VSpacing(1),
			`VSquash(
			    `PushButton(`id("delete_stonith"), Label::DeleteButton())),
			`VStretch()
		    )
		)
	    )
        );
}

any ConfigureNodeDialog () {

    node_conf_Read();
    
    my_SetContents("node_conf", node_conf_getDialog());
    
    any ret = nil;
    while(true) {
    
	Wizard::SelectTreeItem("node_conf");

	ret = UI::UserInput();

        if (ret == `abort || ret == `cancel) {
	    if (ReallyAbort()) return ret;
	    else continue;
	}

	if (ret == `help) {
	    myHelp("node_conf");
	    continue;
	}

	if (ret == `wizardTree)
	    ret = (string)UI::QueryWidget (`id (`wizardTree), `CurrentItem);

	if (ret == `next || ret == `back || contains(DIALOG, (string)ret)) {
	
	    if (!node_conf_Write()) continue;
	    
	    if (ret != `next && ret != `back)
		ret = symbolof(toterm(ret));

	    break;
	}

	y2error("unexpected retcode: %1", ret);
    }

    return ret;
}

/* EOF */
}
