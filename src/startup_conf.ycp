/**
 * Package:	Configuration of heartbeat
 * Authors:	Martin Lazar <mlazar@suse.cz>
 *
 * $Id$
 */

{

textdomain "heartbeat";

import "Label";
import "Wizard";
import "Service";
import "Heartbeat";

include "heartbeat/helps.ycp";
include "heartbeat/common.ycp";


any ConfigureStartUpDialog () {

    boolean boot = Heartbeat::start_daemon;

    term Tbooting = `Frame ( _("Booting"),
		    `Left (
			`RadioButtonGroup( `id ( "server_type" ),
			    `VBox (
				`Left( `RadioButton( `id ("on"), _("On -- Start Heartbeat Server Now and when Booting") ) ),
				`Left( `RadioButton( `id ("off"), _("Off -- Server Only Starts Manually") ) ),
				`VSpacing ( 1 )))));
		
    term Tonoff = `Frame ( _("Switch On and Off"),
		    `Left (
			`HSquash (
			    `VBox (
				`HBox (
				    `Label (_("Current Status: ")),
				    `ReplacePoint (`id ("status_rp"), `Empty()),
				    `HStretch ()
				),
				`PushButton ( `id ( "start_now" ), `opt( `hstretch ), _("Start Heartbeat Server Now") ),
				`PushButton ( `id ( "stop_now" ),  `opt( `hstretch ), _("Stop Heartbeat Server Now") )
			    ))));
    
    term contents = `Empty();
    if (Heartbeat::firstrun) {
	contents = `VBox (Tbooting, `VStretch() );
    } else {
	contents = `VBox (Tbooting, `VSpacing(1), Tonoff, `VStretch() );
    }


    my_SetContents("startup_conf", contents);
    
    UI::ChangeWidget(`id("server_type"), `CurrentButton, boot ? "on" : "off");

    any ret = nil;
    while(true) {
    
	integer status =(integer)Service::Status("heartbeat");

	if (!Heartbeat::firstrun)
	{
	    UI::ChangeWidget (`id ("start_now"), `Enabled, status!=0);
	    UI::ChangeWidget (`id ("stop_now"), `Enabled, status==0);

            UI::ReplaceWidget (`id ("status_rp"),
	    `Label (status==0 ? _("Heartbeat server is running.") : _("Heartbeat server is not running.")));
	}

        ret = UI::UserInput();

        if (ret == `abort || ret == `cancel) {
	    if (ReallyAbort()) return ret;
	    else continue;
	}
 
	if (ret == `next || ret == `back) break;
	
	if (ret == "start_now")  {
	    if (! Service::Start("heartbeat")) {
		Report::Error ( Service::Error());
	    }
	    continue;
	}
	
	if (ret == "stop_now") {
	    if (! Service::Stop("heartbeat")) {
		Report::Error ( Service::Error() );
	    }
	    continue;
	}
	
	if (ret == `help) {
	    myHelp("startup_conf");
	    continue;
	}

	if (ret == `wizardTree)
	    ret = (string)UI::QueryWidget (`id (`wizardTree), `CurrentItem);

	if (contains(DIALOG, (string)ret)) {
	    ret = symbolof(toterm(ret));
	    break;
	}

	y2error("unexpected retcode: %1", ret);
    }

    string boot1 = (string)UI::QueryWidget(`id("server_type"), `CurrentButton);
    if ((boot1 == "off" && boot) || (boot1 == "on" && !boot)) {
	Heartbeat::start_daemon_modified = true;
	Heartbeat::start_daemon = !boot;
    }

    return ret;
}

/* EOF */
}
